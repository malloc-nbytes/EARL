# MIT License

# Copyright (c) 2023 malloc-nbytes

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

### MODULE
module List

import "std/utils.earl" full

### BEGIN FUNCTIONS

### NAME preset
### PARAMETER elem: any
### PARAMETER cap: int
### RETURNS str
### DESCRIPTION
###   Creates a list with elements `elem` of size `cap`.
@pub fn preset(elem, cap) {
    let lst = [];
    for i in 0 to cap {
        lst.append(elem);
    }
    return lst;
}

### NAME from_range
### PARAMETER start: int
### PARAMETER stop: int
### PARAMETER stride: int
### RETURNS list
### DESCRIPTION
###   Takes a list and then returns element within range specified by the parameters `start`, `stop`, `stride`.
@pub fn from_range(start, stop, stride) {
    assert(stride != 0);

    let lst = [];
    let i = start;

    while i <= stop {
        lst.append(i);
        i = i+stride;
    }

    return lst;
}

### NAME fill
### PARAMETER lst: @ref list
### PARAMETER k : any
### RETURNS unit
### DESCRIPTION
###   Fills the given list `lst` with element `k`.
@pub fn fill(@ref lst, k) {
    for i in 0 to len(lst) {
        lst[i] = k;
    }
}

### NAME sumf
### PARAMETER lst: @ref lst
### RETURNS float
### DESCRIPTION
###   Returns the sum all elements in `lst` as a float.
@pub fn sumf(@ref lst) {
    let s = 0.0;
    for i in 0 to len(lst) {
        s = s + lst[i];
    }
    return s;
}

### NAME sumf
### PARAMETER lst: @ref lst
### RETURNS int
### DESCRIPTION
###   Returns the sum all elements in `lst` as an integer.
@pub fn sum(@ref lst) {
    let s = 0;
    for i in 0 to len(lst) {
        s = s + lst[i];
    }
    return s;
}

### NAME find
### PARAMETER lst: @ref lst
### PARAMETER elem: @ref any
### RETURNS any
### DESCRIPTION
###   Takes a reference to a list and a reference to an element and looks for the element find in the given list
###   Returns the index of the first occurrence that `elem` appears in `lst` wrapped in `some`, or `none` if not found.
@pub fn find(@ref lst, @ref elem) {
    let i = 0;
    while i < len(lst) {
        if elem == lst[i] {
            return some(i);
        }
        i += 1;
    }
    return none;
}

### NAME count
### PARAMETER lst: @ref list
### PARAMETER elem: @ref any
### RETURNS int
### DESCRIPTION
###   Counts the number of occurrences that `elem` appears in `lst`.
@pub fn count(@ref lst, @ref elem) {
    let c = 0;
    for i in 0 to len(lst) {
        if elem == lst[i] {
            c = c+1;
        }
    }
    return c;
}

### NAME quicksort
### PARAMETER lst: @ref list
### PARAMETER compar: closure(x1: any, x2: type(x1)) -> bool|int
### RETURNS unit
### DESCRIPTION
###   where =compar= is some ordering function $F(x_1, x_2) \in \{true, false\}$
###
###   and $F$ is defined by
###
###   \[
###   F(x_1, x_2) = \begin{cases}
###         true & \text{if } R(x_1) \ge R(x_2) \\
###         false & \text{if } R(x_1) < R(x_2)
###      \end{cases}
###   \]
###
###   and $R(x)$ is some ranking function that produces a rank of $x$.
###
###   Performs the quicksort sorting algorithm on =lst= and
###   sorts by the comparison closure =compar=.
@pub fn quicksort(@ref lst, compar) {
    let __quicksort = |@ref lst, low, high, @ref compar| {
        let partition = |@ref lst, low, high| {
            let pivot = lst[high];
            let i = low-1;

            for j in low to high {
                if compar(lst[j], pivot) {
                    i = i+1;
                    Utils::swap(lst[i], lst[j]);
                }
            }

            Utils::swap(lst[i+1], lst[high]);

            return i+1;
        };

        if low < high {
            let pi = partition(lst, low, high);
            __quicksort(lst, low, pi-1, compar);
            __quicksort(lst, pi+1, high, compar);
        }
    };

    __quicksort(lst, 0, len(lst)-1, compar);
}

### END FUNCTIONS

# autodoc comments todo
@pub let DEFAULT_INT_ASCEND_QUICKSORT = |x, y| { return y > x; };

@pub let DEFAULT_INT_DESCEND_QUICKSORT = |x, y| { return x > y; };
