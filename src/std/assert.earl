# MIT License

# Copyright (c) 2023 malloc-nbytes

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

module Assert

### Variable
#-- Name: CRASH_ON_FAILURE
#-- Type: bool
#-- Description:
#--   Will crash the program on assertion failures
#--   if set to `true`. Otherwise will print failures
#--   to `stderr`. By default, it is set to `true`.
@pub let CRASH_ON_FAILURE = true;
### End

### Variable
#-- Name: FILE
#-- Type: str
#-- Description:
#--   Should be set in the caller file to set the file location
#--   that the assertions are being called from using `__FILE__`.
#--   *Note*: This variable must be set manually.
@pub let FILE = "";
### End

### Variable
#-- Name: FUNC
#-- Type: str
#-- Description:
#--   Should be set in the caller file in the function
#--   that the assertions are being called from using `__FUNC__`.
#--   *Note*: This variable must be set manually.
@pub let FUNC = "";
### End

@world
fn fail(msg) {
    if FILE == "" {
        panic(__FILE__, ':', __FUNC__, ": ", "the FILE variable has not been set");
    }
    if FUNC == "" {
        panic(__FILE__, ':', __FUNC__, ": ", "the FUNC variable has not been set");
    }

    let loc = "("+FILE+":"+FUNC+") ";

    if CRASH_ON_FAILURE {
        panic("ASSERTION FAILURE: ", loc, '(', msg, ')');
    }

    fprintln(IO::Fd.Err, "ASSERTION FAILURE: ", loc, '(', msg, ')');
}

### Function
#-- Name: is_true
#-- Parameter: cond: bool
#-- Returns: unit
#-- Description:
#--   Tests `cond` for `true`.
@pub fn is_true(cond: bool): unit {
    if !cond {
        fail(__FUNC__ + f"() expected `true` but got `false`");
    }
}
### End

### Function
#-- Name: is_false
#-- Parameter: cond: bool
#-- Returns: unit
#-- Description:
#--   Tests `cond` for `false`.
@pub fn is_false(cond: bool): unit {
    if cond {
        fail(__FUNC__ + "() expected `false` but got `true`");
    }
}
### End

### Function
#-- Name: eq
#-- Parameter: l: any
#-- Parameter: r: any
#-- Returns: unit
#-- Description:
#--   Tests for `l` equal to `r`. *Note*: this function
#--   is type safe to it will not panic with differing types.
@pub fn eq(l: any, r: any): unit {
    if typeof(l) == typeof(none) && typeof(r) == typeof(none) {
        if l.is_none() && r.is_some() {
            fail(__FUNC__ + f"() left = {l}, right = {r}");
        }
        else if l.is_some() && r.is_none() {
            fail(__FUNC__ + f"() left = {l}, right = {r}");
        }
        else if l.is_some() && r.is_some() {
            eq(l.unwrap(), r.unwrap());
        }
    }
    else {
        match l {
            r -> {}
            _ -> {fail(__FUNC__ + f"() left = {l}, right = {r}");}
        }
    }
}
### End

### Function
#-- Name: neq
#-- Parameter: l: any
#-- Parameter: r: any
#-- Returns: unit
#-- Description:
#--   Tests for `l` not equal to `r`. *Note*: this function
#--   is type safe to it will not panic with differing types.
@pub fn neq(l: any, r: any): unit {
    if typeof(l) == typeof(none) && typeof(r) == typeof(none) {
        if l.is_none() && r.is_none() {
            fail(__FUNC__ + "() left = {l}, right = {r}");
        }
        else if l.is_some() && r.is_some() {
            neq(l.unwrap(), r.unwrap());
        }
    }
    else {
        match l {r -> {fail(__FUNC__ + "() left = {l}, right = {r}");}}
    }
}
### End

### Function
#-- Name: not_none
#-- Parameter: k: any
#-- Returns: unit
#-- Description:
#--   Tests for `k` being not `none`. *Note*: this function
#--   is type safe so it will not panic with differing types.
@pub fn not_none(k: any): unit {
    match k {none -> {fail(__FUNC__ + "() expected not `none` but got `none`");}}
}
### End

### Function
#-- Name: is_none
#-- Parameter: k: any
#-- Returns: unit
#-- Description:
#--   Tests for `k` being `none`. *Note*: this function
#--   is type safe so it will not panic with differing types.
@pub fn is_none(k: any): unit {
    match k {
        none -> {}
        _ -> {
            fail(__FUNC__ + "() expected `none` but got `" + str(k) + ": " + str(typeof(k)) + "`");
        }
    }
}
### End
