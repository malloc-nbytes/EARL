# MIT License

# Copyright (c) 2023 malloc-nbytes

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

### MODULE
module Math

import "std/list.earl";

### BEGIN FUNCTIONS

### NAME max
### PARAMETER x: int|float
### PARAMETER y: int|float
### RETURNS int|float
### DESCRIPTION
###   Returns the greater of `x` and `y`.
@pub fn max(x, y) {
    if x >= y {
        return x;
    }
    return y;
}

### NAME min
### PARAMETER x: int|float
### PARAMETER y: int|float
### RETURNS int|float
### DESCRIPTION
###   Returns the minimum of `x` and `y`.
@pub fn min(x, y) {
    if x <= y {
        return x;
    }
    return y;
}

### NAME abs
### PARAMETER x: int|float
### RETURNS int|float
### DESCRIPTION
###   Returns the absolute value of `x`.
@pub fn abs(x) {
    if x < 0 {
        return x * (0-1);
    }
    return x;
}

# Statistical Functions #

### NAME mode
### PARAMETER lst: list
### RETURNS list
### DESCRIPTION
###   This function returns a list of all modes that appear at the
###   highest frequency in the given list in the order modes are found.
@pub fn mode(lst) {

    if len(lst) == 0 {
        return none;
    }

    let count = 0;
    let max_count = 0;
    let lst_modes = [];

    for i in 0 to len(lst) {
        if (List::find(lst_modes, lst[i]) == -1) {

            count = List::count(lst, lst[i]);

            if max_count == count {
                lst_modes.append(lst[i]);
                count = 0;
            }

            if max_count < count {
                lst_modes = [];
                lst_modes.append(lst[i]);
                max_count = count;
                count = 0;
             }
        }
    }
    return lst_modes;
}

### NAME mean
### PARAMETER lst: list
### RETURNS float
### DESCRIPTION
###   Returns the mean (average in a dataset) of a given list.
@pub fn mean(lst) {
    return List::sumf(lst) / len(lst);
}

### NAME median
### PARAMETER lst: list
### RETURNS int
### DESCRIPTION
###   This function sorts and then returns the middle number of a given list
@pub fn median(lst) {
    List::quicksort(lst, List::DEFAULT_INT_ASCEND_QUICKSORT);

    let middle = len(lst)/2;

    if len(lst) % 2 == 0 {
        return (lst[middle-1]+lst[middle])/2.0;
    }

    return lst[middle];
}

### NAME list_min
### PARAMETER lst: list
### RETURNS int|float
### DESCRIPTION
###   Returns the smallest element `lst`.
@pub fn list_min(lst) {
    if len(lst) == 0 {
        return none;
    }

    let min = lst[0];

    for i in 1 to len(lst) {
        if lst[i] < min {
            min = lst[i];
        }
    }

    return min;
}

### NAME list_max
### PARAMETER lst: list
### RETURNS int|float
### DESCRIPTION
###   Returns the largest element `lst`.
@pub fn list_max(lst) {
    if len(lst) == 0 {
        return none;
    }

    let max = lst[0];

    for i in 1 to len(lst) {
        if lst[i] > max {
            max = lst[i];
        }
    }

    return max;
}

### NAME area_of_circle
### PARAMETER r: float|int
### RETURNS float|int
### DESCRIPTION
###   Returns the area of a circle with radius `r`.
@pub @world
fn area_of_circle(r) {
    return PI * (r**2);
}

### NAME area_of_cylinder
### PARAMETER r: float|int
### PARAMETER h: float|int
### RETURNS float|int
### DESCRIPTION
###   Returns the area of a cylinder with radius `r` and height `h`.
@pub @world
fn area_of_cylinder(r, h) {
    return h * PI * (r**2);
}

### NAME area_of_cone
### PARAMETER r: float|int
### PARAMETER h: float|int
### RETURNS float|int
### DESCRIPTION
###   Returns the area of a cone with radius `r` and height `h`.
@pub @world
fn area_of_cone(r, h) {
    return (1/3) * h * PI * (r**2);
}

### NAME floor
### PARAMETER f: float
### RETURNS int
### DESCRIPTION
###   Returns the floor of `f`.
@pub fn floor(f: float): int {
    return int(f);
}

### NAME ceil
### PARAMETER f: float
### RETURNS int
### DESCRIPTION
###   Returns the ceil of `f`.
@pub fn ceil(f: float): int {
    return floor(1+f);
}

### NAME lerp
### PARAMETER a: real
### PARAMETER b: real
### PARAMETER f: real
### RETURNS real
### DESCRIPTION
###   Performs the lerp algorithm from `a` to `b` by `f`.
@pub fn lerp(a: real, b: real, f: real): float {
    return a*(1.-f)+(b*f);
}

### NAME normalize
### PARAMETER x: real
### PARAMETER min: float
### PARAMETER max: float
### RETURNS float
### DESCRIPTION
###   Normalizes `x` to the range of `min` .. `max`.
@pub fn normalize(x: real, min: float, max: float): float {
    return (x-min)/(1-max);
}

### NAME clamp
### PARAMETER value: real
### PARAMETER min: real
### PARAMETER max: real
### RETURNS real
### DESCRIPTION
###   Performs clamp on `value` with `min` and `max`.
@pub fn clamp(value: real, min: real, max: real): real {
    return max(min, min(value, max));
}

### END FUNCTIONS

### BEGIN VARIABLES

### NAME PI
### TYPE float
### DESCRIPTION
###   The mathematical constant for pi.
@pub @const let PI = 3.141592653589793;

### NAME EULER
### TYPE float
### DESCRIPTION
###   The mathematical constant for Euler's number
@pub @const let EULER = 2.718281828459045;

### END VARIABLES
